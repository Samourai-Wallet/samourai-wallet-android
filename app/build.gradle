apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    useLibrary 'org.apache.http.legacy'
    flavorDimensions "versionCode"
    defaultConfig {
        applicationId 'com.samourai.wallet'
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 163
        versionName "0.99.94a"
        // Enabling multidex support.
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "XOR_1", "\""+getApiKey("XOR_1")+"\""
            variant.buildConfigField "String", "XOR_2", "\""+getApiKey("XOR_2")+"\""
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }
    productFlavors {
        production {
            applicationId 'com.samourai.wallet'
            minSdkVersion 21
            targetSdkVersion 28
            versionCode 163
            versionName "0.99.94a"
            resValue "string", "app_name", "Samourai"
            resValue "string", "version_name", "0.99.94a"
        }
        staging {
            applicationId 'com.samourai.wallet.staging'
            minSdkVersion 21
            targetSdkVersion 28
            versionCode 163
            versionName "0.99.95"
            resValue "string", "app_name", "Samourai Staging"
            resValue "string", "version_name", "0.99.95"
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

def getApiKey(key){
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('samourai.api')))
    return props[key]
}


dependencies {
    def lifecycle_version = "2.0.0"

    api fileTree(include: ['*.jar'], dir: 'libs')
    implementation('com.google.zxing:core:3.3.0') {
        transitive = true
    }
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'net.i2p.android.ext:floatingactionbutton:1.9.0'
    implementation 'org.thoughtcrime.ssl.pinning:AndroidPinning:1.0.0'
    implementation 'com.neovisionaries:nv-websocket-client:1.9'
    implementation 'com.baoyz.swipemenulistview:library:1.2.1'
    implementation 'commons-io:commons-io:2.5'
    implementation 'commons-codec:commons-codec:1.4'
    implementation 'org.apache.commons:commons-lang3:3.3'
    implementation 'com.google.guava:guava:20.0'
    implementation 'com.google.code.findbugs:jsr305:1.3.9'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.55'
    implementation 'com.madgag.spongycastle:prov:1.54.0.0'
    implementation 'com.lambdaworks:scrypt:1.4.0'
    implementation 'info.guardianproject.netcipher:netcipher:2.0.0-alpha1'
    implementation 'info.guardianproject.netcipher:netcipher-okhttp3:2.0.0-alpha1'
    implementation 'com.github.mjdev:libaums:0.5.5'
    implementation 'de.mindpipe.android:android-logging-log4j:1.0.3'
    implementation 'com.github.magnusja:java-fs:0.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.yanzhenjie.zbar:camera:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'org.hibernate:hibernate-validator:5.4.3.Final'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation ('com.github.Samourai-Wallet:whirlpool-client:0.23.17') {
        exclude group:'org.hibernate', module:'hibernate-validator'
        exclude group:'com.google.code.findbugs', module:'jsr305'
        exclude group:'com.google.protobuf', module:'protobuf-java'
        exclude group:'net.jcip', module:'jcip-annotations'
        exclude group:'com.squareup.okhttp', module:'okhttp'
        //
        exclude group:'org.apache.commons', module:'commons-lang3'
        exclude group:'org.json', module:'json'
    }
    implementation ('com.github.Samourai-Wallet:extlibj:0.0.11') {
        exclude group:'com.google.code.findbugs', module:'jsr305'
        exclude group:'com.google.protobuf', module:'protobuf-java'
        exclude group:'net.jcip', module:'jcip-annotations'
        exclude group:'com.squareup.okhttp', module:'okhttp'
        //
        exclude group:'org.apache.commons', module:'commons-lang3'
        exclude group:'org.json', module:'json'
    }
    implementation ('io.samourai.code.whirlpool:boltzmann-java:1.0.1') {
        exclude group: 'it.unimi.dsi', module: 'fastutil'
    }
    // temporary fix for building boltzmann-java dependency
    implementation 'it.unimi.dsi:fastutil:7.2.1'
    implementation 'com.github.NaikSoftware:StompProtocolAndroid:1.6.4'
    implementation 'org.java-websocket:Java-WebSocket:1.3.6'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.auth0.android:jwtdecode:1.1.1'
    implementation 'net.sourceforge.streamsupport:streamsupport:1.6.0'
    implementation 'com.github.clans:fab:1.6.4'


    implementation 'com.squareup.okhttp3:okhttp:3.13.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    testImplementation 'junit:junit:4.12'
    implementation project(path: ':tor')
}
