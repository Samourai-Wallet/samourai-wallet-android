apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    useLibrary 'org.apache.http.legacy'
    flavorDimensions "versionCode"
    defaultConfig {
        applicationId 'com.samourai.wallet'
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 183
        versionName "0.99.96i"
        // Enabling multidex support.
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "XOR_1", "\""+getApiKey("XOR_1")+"\""
            variant.buildConfigField "String", "XOR_2", "\""+getApiKey("XOR_2")+"\""
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }
    productFlavors {
        production {
            applicationId 'com.samourai.wallet'
            minSdkVersion 21
            targetSdkVersion 30
            versionCode 183
            versionName "0.99.96i"
            resValue "string", "app_name", "Samourai"
            resValue "string", "version_name", "0.99.96i"
        }
        staging {
            applicationId 'com.samourai.wallet.staging'
            minSdkVersion 21
            targetSdkVersion 30
            versionCode 183
            versionName "0.99.96i"
            resValue "string", "app_name", "Samourai Staging"
            resValue "string", "version_name", "0.99.96i"
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

def getApiKey(key){
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('samourai.api')))
    return props[key]
}


dependencies {

    def lifecycle_version = "2.2.0"
    def topl_android_version = "2.1.0"
    def coroutines_version = '1.3.9'

    api fileTree(include: ['*.jar'], dir: 'libs')
    implementation('com.google.zxing:core:3.3.0') {
        transitive = true
    }


    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation 'net.i2p.android.ext:floatingactionbutton:1.9.0'
    implementation 'org.thoughtcrime.ssl.pinning:AndroidPinning:1.0.0'
    implementation 'com.neovisionaries:nv-websocket-client:1.9'
    implementation 'commons-io:commons-io:2.5'
    implementation 'commons-codec:commons-codec:1.4'
    implementation 'org.apache.commons:commons-lang3:3.3'
    implementation 'com.google.guava:guava:20.0'
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.55'
    implementation 'com.madgag.spongycastle:prov:1.54.0.0'
    implementation 'com.lambdaworks:scrypt:1.4.0'
    implementation 'info.guardianproject.netcipher:netcipher:2.0.0-alpha1'
    implementation 'info.guardianproject.netcipher:netcipher-okhttp3:2.0.0-alpha1'
    implementation 'com.github.mjdev:libaums:0.5.5'
    implementation 'de.mindpipe.android:android-logging-log4j:1.0.3'
    implementation 'com.github.magnusja:java-fs:0.1.3'
    implementation 'com.yanzhenjie.zbar:camera:1.0.0'
    implementation 'com.google.android.material:material:1.3.0-alpha03'
    implementation 'org.hibernate:hibernate-validator:5.4.3.Final'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.budiyev.android:code-scanner:2.1.0'
    implementation ('io.samourai.code.whirlpool:whirlpool-client:0.23.29') {
        exclude group:'org.hibernate', module:'hibernate-validator'
        exclude group:'com.google.code.findbugs', module:'jsr305'
        exclude group:'com.google.protobuf', module:'protobuf-java'
        exclude group:'net.jcip', module:'jcip-annotations'
        exclude group:'com.squareup.okhttp', module:'okhttp'
        //
        exclude group:'org.apache.commons', module:'commons-lang3'
        exclude group:'org.json', module:'json'
        exclude group:'io.samourai.code.wallet', module:'extlibj'
    }
    implementation ('io.samourai.code.wallet:extlibj:0.0.17-fetchWallet') {
        exclude group:'com.google.code.findbugs', module:'jsr305'
        exclude group:'com.google.protobuf', module:'protobuf-java'
        exclude group:'net.jcip', module:'jcip-annotations'
        exclude group:'com.squareup.okhttp', module:'okhttp'
        //
        exclude group:'org.apache.commons', module:'commons-lang3'
        exclude group:'org.json', module:'json'
    }
    implementation ('io.samourai.code.wallet:soroban-client-java:0.0.5') {
        exclude group:'io.samourai.code.wallet', module:'extlibj'
    }
    implementation ('io.samourai.code.wallet:boltzmann-java:1.0.1') {
        exclude group: 'it.unimi.dsi', module: 'fastutil'
    }
    // temporary fix for building boltzmann-java dependency
    implementation 'it.unimi.dsi:fastutil:7.2.1'
    implementation 'com.github.NaikSoftware:StompProtocolAndroid:1.6.4'
    implementation 'org.java-websocket:Java-WebSocket:1.3.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.auth0.android:jwtdecode:1.1.1'
    implementation 'net.sourceforge.streamsupport:streamsupport:1.6.0'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'io.samourai.code.wallet:android-tor-binary:0.4.5.4'

    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    testImplementation 'junit:junit:4.12'

    implementation "io.matthewnelson.topl-android:topl-core-base:$topl_android_version"
    implementation "io.matthewnelson.topl-android:topl-service:$topl_android_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}
